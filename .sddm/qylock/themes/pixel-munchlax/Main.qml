import QtQuick
import QtQuick.Window
import Qt5Compat.GraphicalEffects
import Qt.labs.folderlistmodel
import SddmComponents 2.0

// Munchlax Layout
Rectangle {
    readonly property real s: Screen.height / 768
    id: root; width: Screen.width; height: Screen.height; color: "#25787d"
    property bool isQuickshell: typeof sddm === "undefined" || sddm.hostName === undefined
    property int sessionIndex: (typeof sessionModel !== "undefined" && sessionModel.lastIndex >= 0) ? sessionModel.lastIndex : 0
    property int userIndex: userModel.lastIndex >= 0 ? userModel.lastIndex : 0
    property real ui: 0

    // Theme Colors
    readonly property color mTeal: "#50dfd4"
    readonly property color mCream: "#fcf8eb"
    readonly property color mOrange: "#f4a261"

    FolderListModel { id: fontFolder; folder: Qt.resolvedUrl("font"); nameFilters: ["*.ttf", "*.otf"] }
    FontLoader { id: pf; source: fontFolder.count > 0 ? "font/" + fontFolder.get(0, "fileName") : "" }

    ListView { id: sessionHelper; model: typeof sessionModel !== "undefined" ? sessionModel : null; currentIndex: root.sessionIndex; opacity: 0; width: 100; height: 100; z: -100; delegate: Item { property string sName: model.name || "" } }
    ListView { id: userHelper; model: typeof userModel !== "undefined" ? userModel : null; currentIndex: root.userIndex; opacity: 0; width: 100; height: 100; z: -100; delegate: Item { property string uName: model.realName || model.name || ""; property string uLogin: model.name || "" } }

    // Auto-focus fix for Quickshell (Loader does not propagate focus: true)
    Timer { interval: 300; running: true; onTriggered: pwd.forceActiveFocus() }

    Component.onCompleted: fadeAnim.start()
    NumberAnimation { id: fadeAnim; target: root; property: "ui"; from: 0; to: 1; duration: 600; easing.type: Easing.OutSine }

    Loader { anchors.fill: parent; source: "BackgroundVideo.qml" }

    // Bottom Overlay
    Rectangle {
        anchors.bottom: parent.bottom; anchors.left: parent.left; anchors.right: parent.right
        height: 360 * s; opacity: root.ui
        gradient: Gradient { GradientStop { position: 0.0; color: "transparent" } GradientStop { position: 1.0; color: "#d0000000" } }
    }

    // Clock View
    Column {
        anchors.top: parent.top; anchors.right: parent.right; anchors.margins: 60 * s
        spacing: 4 * s; opacity: root.ui

        Item {
            anchors.right: parent.right
            width: cd.implicitWidth; height: cd.implicitHeight
            Text { text: cd.text; color: "#80000000"; font: cd.font; x: 2*s; y: 2*s }
            Text {
                id: cd
                text: Qt.formatDate(new Date(), "dddd, MMMM d").toUpperCase()
                color: root.mTeal; font.family: pf.name; font.pixelSize: 18 * s; font.letterSpacing: 6 * s
            }
        }

        Item {
            anchors.right: parent.right
            width: ct.implicitWidth; height: ct.implicitHeight
            Text { text: ct.text; color: "#80000000"; font: ct.font; x: 3*s; y: 3*s }
            Text {
                id: ct
                text: Qt.formatTime(new Date(), "HH:mm")
                color: root.mCream; font.family: pf.name; font.pixelSize: 96 * s
                Timer { interval: 1000; running: true; repeat: true; onTriggered: ct.text = Qt.formatTime(new Date(), "HH:mm") }
            }
        }
    }

    // Login View
    Item {
        anchors.bottom: parent.bottom; anchors.left: parent.left; anchors.margins: 60 * s
        width: 300 * s; height: 160 * s; opacity: root.ui

        Column {
            anchors.bottom: parent.bottom; anchors.left: parent.left; spacing: 20 * s; width: 280 * s

            Item {
                anchors.left: parent.left
                width: un.implicitWidth; height: un.implicitHeight
                Text { text: un.text; color: "#80000000"; font: un.font; x: 2*s; y: 2*s }
                Text {
                    id: un
                    text: ((userHelper.currentItem && userHelper.currentItem.uName) ? userHelper.currentItem.uName : (userModel.lastUser || "User")).toUpperCase()
                    color: root.mCream; font.family: pf.name; font.pixelSize: 22 * s; font.letterSpacing: 4 * s
                }
                MouseArea { anchors.fill: parent; cursorShape: Qt.PointingHandCursor; onClicked: { if (typeof userModel !== "undefined" && userModel.rowCount() > 0) root.userIndex = (root.userIndex + 1) % userModel.rowCount() } }
            }

            Item {
                width: parent.width; height: 36 * s
                Rectangle { anchors.bottom: parent.bottom; anchors.left: parent.left; width: parent.width; height: 1 * s; color: root.mTeal; opacity: pwd.activeFocus ? 1.0 : 0.3; Behavior on opacity { NumberAnimation {duration: 300} } }
                Rectangle { anchors.bottom: parent.bottom; anchors.left: parent.left; width: pwd.activeFocus ? parent.width : 0; height: 2 * s; color: root.mOrange; Behavior on width { NumberAnimation {duration: 300; easing.type: Easing.OutExpo} }
                    layer.enabled: true; layer.effect: Glow { color: root.mOrange; radius: 6; spread: 0.4 }
                }
                TextInput {
                    id: pwd; anchors.fill: parent; color: root.mCream; font.family: pf.name; font.pixelSize: 18 * s; font.letterSpacing: 4 * s
                    echoMode: TextInput.Password; onTextEdited: err.text = ""; passwordCharacter: "─"; focus: true; clip: true; horizontalAlignment: TextInput.AlignLeft; verticalAlignment: TextInput.AlignVCenter
                    cursorVisible: false; cursorDelegate: Item { width: 0; height: 0 }
                    selectionColor: root.mOrange
                    property bool wasClicked: false
                    onActiveFocusChanged: if (!activeFocus && text.length === 0) wasClicked = false
                    Keys.onReturnPressed: doLogin(); Keys.onEnterPressed: doLogin()
                }
                Text {
                    anchors.verticalCenter: parent.verticalCenter; anchors.left: parent.left; text: "password"; color: root.mTeal; font.family: pf.name; font.pixelSize: 14 * s; font.letterSpacing: 4 * s
                    opacity: pwd.text.length === 0 ? 0.4 : 0
                    Behavior on opacity { NumberAnimation { duration: 400; easing.type: Easing.InOutSine } }
                }
                Rectangle {
                    id: customCursor
                    width: 2 * s; height: 20 * s
                    color: root.mOrange
                    anchors.verticalCenter: parent.verticalCenter
                    x: pwd.cursorRectangle.x
                    visible: pwd.focus && (pwd.text.length > 0 || pwd.wasClicked)
                    SequentialAnimation {
                        loops: Animation.Infinite; running: customCursor.visible
                        NumberAnimation { target: customCursor; property: "opacity"; from: 1; to: 0.05; duration: 450 }
                        NumberAnimation { target: customCursor; property: "opacity"; from: 0.05; to: 1; duration: 450 }
                    }
                }
                MouseArea {
                    anchors.fill: parent
                    onClicked: {
                        pwd.forceActiveFocus()
                        pwd.wasClicked = true
                    }
                }
            }

            Item {
                anchors.left: parent.left; width: 140 * s; height: 36 * s
                Rectangle { anchors.fill: parent; color: sbm.containsMouse ? root.mTeal : "transparent"; border.color: root.mTeal; border.width: 1; radius: 4 * s; Behavior on color { ColorAnimation { duration: 150 } } }
                Text { anchors.centerIn: parent; text: "LOGIN"; color: sbm.containsMouse ? "#000" : root.mCream; font.family: pf.name; font.pixelSize: 12 * s; font.letterSpacing: 4 * s; Behavior on color { ColorAnimation { duration: 150 } } }
                MouseArea { id: sbm; anchors.fill: parent; hoverEnabled: true; cursorShape: Qt.PointingHandCursor; onClicked: doLogin() }
            }

            Text { id: err; text: ""; height: 12 * s; verticalAlignment: Text.AlignBottom; color: "#ff4444"; anchors.left: parent.left; font.family: pf.name; font.pixelSize: 12 * s }
        }
    }

    // Power Box
    Row {
        anchors.bottom: parent.bottom; anchors.right: parent.right; anchors.margins: 40 * s; spacing: 20 * s; opacity: root.ui
        Repeater {
            model: [{l: (sessionHelper.currentItem && sessionHelper.currentItem.sName ? sessionHelper.currentItem.sName : "Session").toUpperCase(), a: 2}, {l: "RESTART", a: 0}, {l: "SHUT DOWN", a: 1}]
            delegate: Item {
                visible: modelData.a === 2 ? !root.isQuickshell : true
                width: pmt.implicitWidth + 24 * s; height: 28 * s
                Rectangle { anchors.fill: parent; color: "transparent"; border.color: root.mTeal; border.width: 1 * s; opacity: pm.containsMouse ? 1.0 : 0.4; radius: 4 * s; Behavior on opacity { NumberAnimation { duration: 150 } } Rectangle { anchors.fill: parent; anchors.margins: 1 * s; color: modelData.a === 2 ? root.mOrange : root.mTeal; radius: 3 * s; opacity: pm.containsMouse ? 0.3 : 0; Behavior on opacity { NumberAnimation { duration: 150 } } } }
                Text { id: pmt; anchors.centerIn: parent; text: modelData.l; color: root.mCream; font.family: pf.name; font.pixelSize: 10 * s; font.letterSpacing: 2 * s }
                MouseArea { id: pm; anchors.fill: parent; hoverEnabled: true; cursorShape: Qt.PointingHandCursor; onClicked: { if (modelData.a === 0) { if (typeof sddm !== "undefined") sddm.reboot() } else if (modelData.a === 1) { if (typeof sddm !== "undefined") sddm.powerOff() } else if (typeof sessionModel !== "undefined" && sessionModel.rowCount() > 0) root.sessionIndex = (root.sessionIndex + 1) % sessionModel.rowCount() } }
            }
        }
    }

    Connections {
        target: typeof sddm !== "undefined" ? sddm : null
        function onLoginFailed() { err.text = "ACCESS DENIED"; pwd.text = ""; pwd.focus = true }
    }

    function doLogin() { var u = (userHelper.currentItem && userHelper.currentItem.uLogin) ? userHelper.currentItem.uLogin : (typeof userModel !== "undefined" ? userModel.lastUser : ""); if (typeof sddm !== "undefined") sddm.login(u, pwd.text, root.sessionIndex) }
}
